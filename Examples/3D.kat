#------------------------------------------------------------------
# 3D.kat test file for Finesse 1.1
# (Pound-Drever-Hall in 3D)
# http://www.gwoptics.org/finesse/
# 01.11.2013
#
#                                                        
#                        m1                               m2           
#         .-----.       .-.                               .-.          
#         |     |       | |      . . . . . . . . . .      | |          
# --> n1  | EOM | n2 n3 | |  n4  .      s_cav      .  n5  | | 
#         |     |       | |      . . . . . . . . . .      | |          
#         `-----'       | |                               | |          
#                       `-'                               `-'          
#------------------------------------------------------------------

l i1 1 0 n0                 # laser P=1W f_offset=0Hz
s s0 1 n0 n1
mod eo1 40k 0.3 3 pm n1 n2  # phase modulator f_mod=40kHz
                            # midx=0.3 order=3 

s s1 1 n2 n3

                            # a Fabry-Perot cavity
m m1 0.9 0.0001 0 n3 n4     # mirror R=0.9 T=0.0001 phi=0
s s_cav 1200 n4 n5          # space L=1200
m m2 0.8 0.1 0 n5 dump      # mirror R=0.8 T=0.1 phi=0

pd1 inphase 40k 0 n3        # photo detecor + mixer 
                            # f_demod=40kHz phase=0
xaxis m1 phi lin -80 80 80  # xaxis: phi at mirror m1
                            # from -80 to 80 (80 steps)
x2axis m2 r lin 0.5 0.9 80	# second xaxis (for 3D plot)
                            # r at mirror m2, 0.5 to 0.9

func y = (-1)*$x2+1         # compute y=-1*x2+1 (x2 as x2axis)
put m2 t $y                 # put $y into m2 transmission

yaxis abs                   # yaxis: plot 'as is'

multi
x3axis i1 f lin -1 10 1000

gnuterm x11                 # gnuplot terminal: x11

GNUPLOT                     # some Gnuplot settings for
set view 70, 220, ,         # the 3D plot ...
set hidden3d
set pm3d at s hidden3d 100 solid
END




