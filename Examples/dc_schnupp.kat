#------------------------------------------------------------------
# dc-schnupp.kat test file for Finesse 1.1
# http://www.gwoptics.org/finesse/
# 01.11.2013
# 
# The "#" is used for comment lines. 
#                                                                     
# Power recycled Michelson Interferometer 
#                 with Schnupp Modulation :                              
#                                                                     
#                                                                     
#                                _____                                
#                               <_____>  north-m                      
#                                                                     
#                                 n7                                    
#                                                                     
#                               north-s                     
#                                                                     
#                                       bs1            east-m         
#              .-.                n4 .'>              .-.             
#    .---.     | |                 .'.'               | |             
# n0 |EOM| n1  | | n2 west-s  n3 .'.' n5   east-s  n8 | |             
#    `---'     | |              <.'                   | |             
#              `-'                                    `-'             
#                                 n6                                  
#                                                                     
#                                  |                                  
#                                  |                                  
#                                  |                                  
#                                  V    
#                              
#                            photo detectors                              
#------------------------------------------------------------------

l i1 1 0 nin                     # laser 1W

mod eo1 1M 0.45 1 pm nin n0      # Schnupp (in-line) modulation

s s1 1 n0 n1

m west-m 0.9 0.1 0 n1 n2        # power recycling mirror
s west-s 1 n2 n3                # west arm

bs bs1 0.5 0.5 0 45 n3 n4 n5 n6 # beam splitter (50/50)

s north-s 1201 n4 n7            # north arm
m north-m 1 0 0 n7 dump         # north end mirror      

s east-s 1199 n5 n8             # east arm
m east-m 1 0 90 n8 dump         # east end mirror

pd1 out1 1M 50 n6               # output photodiode + mixer

# tuning the end mirrors in differential mode
xaxis north-m phi lin -20 20 1000
func dm = 90-$x1
noplot dm
put east-m phi $dm
yaxis abs

