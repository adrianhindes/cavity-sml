#-----------------------------------------------------------------------
# pdh-signal.kat test file for Finesse 1.1
# (Error signal of the Pound-Drever-Hall signal)
#
# http://www.gwoptics.org/finesse/ 
# 01.11.2013
# 								  
# The "#" is used for comment lines. 				  
#
#                                                        
#                        m1                               m2           
#         .-----.       .-.                               .-.          
#         |     |       | |      . . . . . . . . . .      | |          
# --> n1  | EOM | n2 n3 | |  n4  .      s_cav      .  n5  | | 
#         |     |       | |      . . . . . . . . . .      | |          
#         `-----'       | |                               | |          
#                       `-'                               `-'          
#------------------------------------------------------------------

## reflectivity of first mirror set to 0.9 to get a `nice' plot
## the setting is different in `pdh.kat'!           


l i1 1 0 n0                 # laser P=1W f_offset=0Hz
s s0 1 n0 n1
mod eo1 40k 0.3 3 pm n1 n2  # phase modulator f_mod=40kHz
                            # midx=0.3 order=3 
s s1 1 n2 n3
                            # a Fabry-Perot cavity
m m1 0.9 0.0001 0 n3 n4     # mirror R=0.9 T=0.0001 phi=0
s s_cav 1200 n4 n5          # space L=1200
m m2 1 0 0 n5 dump          # mirror R=0.8 T=0.1 phi=0

pd1 inphase 40k 0 n3        # photo diode + mixer 
                            # f_demod=40kHz phase=0
pd1 quadrature 40k 90 n3    # photo diode + mixer 
                            # f_demod=40kHz phase=90degrees
xaxis m2 phi lin -90 90 400 # xaxis: tune mirror m2 
                            # from -90 to 90 (400 steps)
yaxis abs                   # plot `as is'
